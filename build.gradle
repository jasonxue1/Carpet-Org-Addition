import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'net.fabricmc.fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

// 时间戳
def timeStamp = DateTimeFormatter.ofPattern("yyMMddHHmm").format(LocalDateTime.now())
version = "v${project.mod_version}-${timeStamp}"

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    implementation "net.fabricmc:fabric-loader:${project.loader_version}"
    implementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    // Carpet: https://github.com/gnembon/fabric-carpet
    implementation(files("lib/fabric-carpet-26.1-beta-1+v251217.jar"))
    // implementation("curse.maven:carpet-349239:${project.carpet_version}")
    // MixinExtras: https://github.com/LlamaLad7/MixinExtras
    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixinextras_version}")))
    testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
}

processResources {
    def depend_version = minecraft_version
            .replace("snapshot-", "alpha.")
            .replace("pre-", "pre.")
            .replace("rc-", "rc.")
    inputs.property "version", mod_version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "loader_version", loader_version
    inputs.property "depend_version", depend_version
    inputs.property "buildTimestamp", timeStamp
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": mod_version,
                "minecraft_version": minecraft_version,
                "loader_version": loader_version,
                "depend_version": depend_version,
                "buildTimestamp": timeStamp
    }
}

test {
    useJUnitPlatform()
}

def targetJavaVersion = 25
tasks.withType(JavaCompile).configureEach {
    // 确保编码设置为UTF-8，无论系统默认编码是什么，这修复了某些特殊字符无法正确显示的边缘情况
    // 详见：http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // 如果生成了Javadoc，则在该任务中也必须指定此设置
    it.options.encoding = "UTF-8"
    it.options.release = targetJavaVersion
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

jar {
    from("LICENSE") {
        rename { "${it}" }
    }
    // 在构建jar时排除org.util包
    exclude "org/util/**"
    // 构建模组前自动单元测试
    dependsOn test
    archiveBaseName = "${archives_base_name}-mc${minecraft_version}"
}

loom {
    runs {
        client {
            programArgs(["--username", "Player789"])
            vmArgs(
                    // 抑制jvm警告
                    "-XX:+EnableDynamicAgentLoading",
                    "--enable-native-access=ALL-UNNAMED",
                    "--sun-misc-unsafe-memory-access=allow",
                    // 启用调试工具
                    "-DMC_DEBUG_ENABLED=true",
                    // 在日志中输出命令执行出错时的详细信息
                    "-DMC_DEBUG_VERBOSE_COMMAND_ERRORS=true",
                    // 导出Mixin类
                    "-Dmixin.debug.export=true"
            )
        }
    }
    accessWidenerPath = file("src/main/resources/carpet-org-addition.accesswidener")
}

fabricApi {
    configureDataGeneration() {
        client = true
    }
}
